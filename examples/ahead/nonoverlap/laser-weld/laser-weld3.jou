
# **************************************************************************************************
#
#   Create a welded geometry with a dogbone shape and partial penetration weld at center
#
# **************************************************************************************************


# --------------------------------------------------------------------------------------------------
#  Initialize Cubit  -------------------------------------------------------------------------------
# --------------------------------------------------------------------------------------------------

reset



# --------------------------------------------------------------------------------------------------
#  Global constants  -------------------------------------------------------------------------------
# --------------------------------------------------------------------------------------------------

#{pi = 3.14159265358979323846264338327950288419716939937510582}



# --------------------------------------------------------------------------------------------------
#  Create holder geometry --------------------------------------------------------------------------
# --------------------------------------------------------------------------------------------------

#  Create and position holder blank ----------------------------------------------------------------

#{specimen_length = 0.3}

#{specimen_width = 0.1}

#{specimen_thickness = 0.1}

create brick x {specimen_width} y {specimen_length} z {specimen_thickness}
move volume 1 x {specimen_width/2} y {specimen_length/2} z {specimen_thickness/2}



# --------------------------------------------------------------------------------------------------
#  Cut notch ---------------------------------------------------------------------------------------
# --------------------------------------------------------------------------------------------------

#  Define notch radius based on a scale value and calculate inner mesh refinement cylinder around --
#  crack(outer_notch) ------------------------------------------------------------------------------

#{notch_depth = 0.05}
#{notch_radius = 0.008/2}
#{outer_notch =0.017/2}
#{center_notch_tip = specimen_thickness - notch_depth + notch_radius}


#  Cut notch tip -----------------------------------------------------------------------------------

create cylinder radius {notch_radius} height {specimen_width}
volume 2 rotate 90 about y

volume 2 move z {center_notch_tip}
volume 2 move x {specimen_width/2.}

webcut volume 2 yplane

webcut volume 1 tool volume 3

delete volume 2 3 4

compress ids



#  Cut notch ---------------------------------------------------------------------------------------

create brick x {specimen_width} y {notch_radius*2.} z {notch_depth - notch_radius}
volume 2 move x {specimen_width/2.}
volume 2 move z {specimen_thickness - (notch_depth - notch_radius)/2.}
webcut volume 1 tool volume 2

delete volume 2 3

compress ids



# Create lower weld geometry -----------------------------------------------------------------------

#{angle_notch = 40}

webcut volume 1 with plane zplane offset {center_notch_tip} rotate -{angle_notch} about x center 0 0 {center_notch_tip}



# Create upper weld geometry -----------------------------------------------------------------------

#  Define the nominal size of the weld at the outer surface in the y direction - divide by two due -
#  to y-plane symmetry -----------------------------------------------------------------------------

#{half_weld_width_surface=0.0375/2}

create cylinder radius {half_weld_width_surface} height {specimen_width}
volume 3 rotate 90 about y
volume 3 move x {specimen_width/2.}
webcut volume 2 tool volume 3
delete volume 3

compress ids



#  Create middle weld geometry ---------------------------------------------------------------------

#{angle_weld_upper = pi * 45 / 180}
#{angle_weld_lower = pi * angle_notch / 180}
#{point_intersection = center_notch_tip + sqrt(outer_notch^2 - notch_radius^2)}

webcut volume 2 with general plane location {specimen_width} {notch_radius} {point_intersection} location 0 {notch_radius} {point_intersection} location 0 {half_weld_width_surface*cos(angle_weld_upper)} {half_weld_width_surface*sin(angle_weld_upper)}

unite volume 1 4

unite volume 2 3

compress ids



#  Create other half of specimen -------------------------------------------------------------------

volume all copy reflect y

merge tolerance 5.0e-6
imprint tolerant volume all
merge volume all
merge tolerance 5.000000e-04



#  Unite weld halves -------------------------------------------------------------------------------

unite volume 2 4

compress ids



#  Make cuts for Shwarz ----------------------------------------------------------------------------

#{nElementsThroughThickness = 6}

#{sizeElementCoarse = (specimen_thickness) / (nElementsThroughThickness)}

#{widthRegionWeld = 12.0 * sizeElementCoarse}

webcut volume 1 with plane yplane offset {widthRegionWeld}

webcut volume 3 with plane yplane offset -{widthRegionWeld}

#{widthOverlap = 0.0 * sizeElementCoarse}

webcut volume 3 with plane yplane offset -{widthRegionWeld + widthOverlap}

webcut volume 4 with plane yplane offset {widthRegionWeld + widthOverlap}

compress ids



#  Merge geometries --------------------------------------------------------------------------------

merge tolerance 5.0e-6
imprint tolerant volume all
merge volume all
merge tolerance 5.000000e-04



# ==================================================================================================
#   Mesh specimen ----------------------------------------------------------------------------------
# ==================================================================================================

# --------------------------------------------------------------------------------------------------
#  Create exodus blocks ----------------------------------------------------------------------------
# --------------------------------------------------------------------------------------------------

set duplicate block elements on


# Create weld bead block ---------------------------------------------------------------------------

block 1 volume 2

block 1 name "bead_weld"

block 2 volume 5

block 2 name "region_weld_0"

block 5 volume 1 4

block 55 volume 1

block 55 name "region_weld_1"

block 11 volume 1 2 5

block 11 name "gauge"


# Create holder-0 block ----------------------------------------------------------------------------

block 3 volume 3

block 3 name "holder_0"


# Create holder-1 block ----------------------------------------------------------------------------

block 7 volume 4

block 7 name "holder_1"


# --------------------------------------------------------------------------------------------------
#  Set mesh parameters -----------------------------------------------------------------------------
# --------------------------------------------------------------------------------------------------

##{ratioSizeCoarseFine = 9} #IKT 11/5/2024: this was orig resolution
##{ratioSizeCoarseFine = 2}
#{ratioSizeCoarseFine = 2}
#{sizeElementFine = (sizeElementCoarse) / (ratioSizeCoarseFine)}
##{ratioSizeCoarseMedium = 3} #IKT 11/5/2024: this was orig resolution
#{ratioSizeCoarseMedium = 1}
#{sizeElementMedium = (sizeElementCoarse) / (ratioSizeCoarseMedium)}

# --------------------------------------------------------------------------------------------------
#  Mesh for Shwarz coupling computation ------------------------------------------------------------
# --------------------------------------------------------------------------------------------------

#  Weld region domain ------------------------------------------------------------------------------

volume all size {sizeElementFine}

mesh volume 1 2 5

reset nodeset

set large exodus file on
export mesh "gauge.g" dimension 3 block 1 2 55 overwrite

delete mesh volume all propagate


#  Holder 0 domain ---------------------------------------------------------------------------------

volume 3 size {sizeElementCoarse}

mesh volume 3

reset nodeset

set large exodus file on
export mesh "holder-0.g" dimension 3 block 3 overwrite


#  Holder 1 domain ---------------------------------------------------------------------------------

volume 4 size {sizeElementCoarse}

mesh volume 4

reset nodeset

set large exodus file on
export mesh "holder-1.g" dimension 3 block 7 overwrite

#IKT 11/24/2024: the following is a hack.  For some reason, I can't seem to set
#sidesets correctly in the above code
reset
import mesh geometry "holder-0.g"
nodeset 1 surface 6
nodeset 1 name "nodeset_negative_y"
nodeset 2 surface 2
nodeset 2 name "nodeset_negative_x"
nodeset 3 surface 4
nodeset 3 name "nodeset_negative_z"
sideset 1 surface 6
sideset 1 name "sideset_negative_y"
sideset 2 surface 3
sideset 2 name "sideset_positive_y"
#refine volume all numsplit 1
block all element type HEX8
export mesh "holder-0.g" block all overwrite

reset
import mesh geometry "holder-1.g"
nodeset 1 surface 6
nodeset 1 name "nodeset_positive_y"
nodeset 2 surface 3
nodeset 2 name "nodeset_negative_x"
nodeset 3 surface 4
nodeset 3 name "nodeset_negative_z"
sideset 1 surface 2
sideset 1 name "sideset_negative_y"
sideset 2 surface 6
sideset 2 name "sideset_positive_y"
#refine volume all numsplit 1
block all element type HEX8
export mesh "holder-1.g" block all overwrite

reset
import mesh geometry "gauge.g"
nodeset 1 surface 6 10 14
nodeset 1 name "nodeset_negative_x"
nodeset 2 surface 2 9 13
nodeset 2 name "nodeset_negative_z"
nodeset 3 surface 18
nodeset 3 name "nodeset_negative_y"
nodeset 4 surface 2
nodeset 4 name "nodeset_positive_y"
sideset 1 surface 18
sideset 1 name "sideset_negative_y"
sideset 2 surface 2
sideset 2 name "sideset_positive_y"
block all element type HEX8
export mesh "gauge.g" block all overwrite






